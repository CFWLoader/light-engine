# 基础着色流程

- [Open-GL教学源码地址](https://github.com/opengl-tutorials/ogl)

[Open-GL教程8](http://www.opengl-tutorial.org/cn/beginners-tutorials/tutorial-8-basic-shading/)中介绍`基本着色`算法，由于假定读者已经较为熟悉Open-GL的一些操作，并且该部分着重修改`Shader`部分的源码而未在教程中表现出来，需要消耗一些时间去理解内容。

首先，直接贴上附个人注释的`顶点shader代码`：

``` glsl
#version 330 core

// 不要给长长的变量名吓到
// 三组数据，顶点、uv以及顶点向量
layout(location = 0) in vec3 vertexPosition_modelspace;
layout(location = 1) in vec2 vertexUV;
layout(location = 2) in vec3 vertexNormal_modelspace;

// 这里的数据会送到片元着色器
out vec2 UV;
// 世界坐标下的顶点坐标
out vec3 Position_worldspace;
// 摄像机空间下的向量
out vec3 Normal_cameraspace;
// 摄像机(0,0,0)下的视线向量
out vec3 EyeDirection_cameraspace;
// 摄像机空间下的光线向量
out vec3 LightDirection_cameraspace;

// 整个MVP矩阵
uniform mat4 MVP;
// 转换到摄像机空间的view矩阵
uniform mat4 V;
// 转换到全局坐标的Model矩阵
uniform mat4 M;
// 世界坐标系下的点光源坐标
uniform vec3 LightPosition_worldspace;

/**
 * 计算面片上面最终的颜色，只需要用到世界坐标和摄像机调整后的坐标，用于计算最终显示出来的颜色
 * 因此计算面片的颜色时不需要投影矩阵，但记住原本的顶点坐标仍然先要按MVP过程计算
 */
void main(){

    
	// 标准的局部坐标MVP转换到最终摄像机下投影坐标
	gl_Position =  MVP * vec4(vertexPosition_modelspace,1);
	
	// 通过M矩阵计算出世界坐标
	Position_worldspace = (M * vec4(vertexPosition_modelspace,1)).xyz;
	
	// 摄像机空间下，顶点到摄像机(0,0,0)的向量，得出视线向量
	vec3 vertexPosition_cameraspace = ( V * M * vec4(vertexPosition_modelspace,1)).xyz;
	EyeDirection_cameraspace = vec3(0,0,0) - vertexPosition_cameraspace;

	// 计算摄像机空间下顶点到光线的向量，这里没有从局部到世界的转换，直接使用点光源坐标作为世界坐标
	vec3 LightPosition_cameraspace = ( V * vec4(LightPosition_worldspace,1)).xyz;
    // 光线到顶点的向量=光源向量(这里直接将光源坐标当作视线原点到光源的向量)+顶点到摄像机的向量
    // 画个图，用高中的向量知识就能理解
	LightDirection_cameraspace = LightPosition_cameraspace + EyeDirection_cameraspace;
	
	// 转换到摄像机空间下的顶点法线向量
    // 目前只能正确计算出Model矩阵缩放为1的法线
	Normal_cameraspace = ( V * M * vec4(vertexNormal_modelspace,0)).xyz;
	
	// 常规的UV数据输出
	UV = vertexUV;
}
```

整个顶点着色器主要的逻辑是先将`片元着色器`所需的各类向量和转换好的坐标计算完毕，最终顶点颜色的计算就在`片元着色器`中完成。接下来分析片元着色器的代码：

``` glsl
#version 330 core

// 从FS过来的数据
in vec2 UV;
in vec3 Position_worldspace;
in vec3 Normal_cameraspace;
in vec3 EyeDirection_cameraspace;
in vec3 LightDirection_cameraspace;

// 最终输出的颜色
out vec3 color;

// 整个网格的常量
// 贴图
uniform sampler2D myTextureSampler;
// MV矩阵
uniform mat4 MV;
// 全局光照的世界坐标
uniform vec3 LightPosition_worldspace;

void main(){

	// 光照配置，光的颜色以及光源功率，可以设置成uniform被上一阶段控制
	vec3 LightColor = vec3(1,1,1);
	float LightPower = 50.0f;
	
	// 材质属性
    // 漫反射基础颜色，从UV中获取
	vec3 MaterialDiffuseColor = texture( myTextureSampler, UV ).rgb;
    // 环境光基础颜色，使得无光照处可以稍微有点显示，而不是全黑
	vec3 MaterialAmbientColor = vec3(0.1,0.1,0.1) * MaterialDiffuseColor;
    // 镜面反射基础颜色
	vec3 MaterialSpecularColor = vec3(0.3,0.3,0.3);

	// 光源距离
	float distance = length( LightPosition_worldspace - Position_worldspace );

	// 世界坐标系下面片的法向量
	vec3 n = normalize( Normal_cameraspace );
	// 面片指向光源的向量
	vec3 l = normalize( LightDirection_cameraspace );
	// 面片法向量与面片指向光照的余弦值
	// 当余弦值为负值时，当作0处理
	float cosTheta = clamp( dot( n,l ), 0,1 );
	
	// 顶点到视线(0,0,0)的视线向量
	vec3 E = normalize(EyeDirection_cameraspace);
	// 光源指向面片的向量，根据面片法向量计算出反射向量
	vec3 R = reflect(-l,n);
	// 视线向量与发射向量的余弦值，0-1之间，防止负值的出现
	float cosAlpha = clamp( dot( E,R ), 0,1 );
	
	color = 
		// 环境光，直接用上述算出的值当作漫反射的结果
		MaterialAmbientColor +
		// 漫反射的结果
		MaterialDiffuseColor * LightColor * LightPower * cosTheta / (distance*distance) +
		// 镜面反射的结果
		MaterialSpecularColor * LightColor * LightPower * pow(cosAlpha,5) / (distance*distance);

}
```